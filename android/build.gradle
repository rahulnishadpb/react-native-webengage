apply plugin: 'com.android.library'

// NOTE: This buildscript block is NOT used when this library is consumed by apps.
// Apps use their own AGP version to build all libraries. This is only for standalone library development.
buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.0'
    }
}
android {
    compileSdkVersion 33
    namespace "com.webengage"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        manifestPlaceholders = [we_rn_version: readVersion()]
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    google()
}

String readVersion() {
    def version = new Properties()
    def stream
    try {
        stream = new FileInputStream(new File(projectDir, 'version.properties'))
        version.load(stream)
    } catch (FileNotFoundException ignore) {
    } finally {
        if (stream != null) stream.close()
    }
    // safety defaults in case file is missing
    if(!version['major']) version['major'] = "1"
    if(!version['minor']) version['minor'] = "0"
    if(!version['patch']) version['patch'] = "0"
    return "${version['major']}.${version['minor']}.${version['patch']}"
}

dependencies {
    compileOnly 'com.facebook.react:react-native:+'
    api 'com.webengage:android-sdk:[4.4,)'

}
